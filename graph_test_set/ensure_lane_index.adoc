## Test: ensure_lane_index

#### Test description

If more than one R1/R2/I1/I2 in a set, then ensure lane indices are specified.

If more than one set of R1/R2/I1/I2 SEQUENCE_FILEs belong to a single  process (sequencing experiment) AND those files lack any lane_index, then this snippet returns an error.


#### The test
[source,cypher]
----
/////////////////////////////////////////
// If more than one R1/R2/I1/I2 in a set,
// ensure lane index is specified.
// 2020-05-06; Ray Stefancsik
MATCH (p:process)-[:DERIVED_FILES]->(f:sequence_file)
WHERE f.read_index IN [ 'read1', 'read2', 'index1', 'index2']
WITH
      p.`process_core.process_id` AS process_id
    , f.read_index AS read_index
    , collect(f.`file_core.file_name`) AS file_names
    , collect(f.lane_index) AS lane_indeces
    , count(*) AS fileCount
WHERE fileCount > 1 // Check that there are multiple sequence files for each type [ 'read1', 'read2', 'index1', 'index2'] belonging to a single process so that lane_index is required.
  AND size(lane_indeces) <> fileCount //The total number of sequence files and the number of sequence files that have a lane_index in each category must be equal to pass the test.
RETURN process_id, read_index, fileCount - size(lane_indeces) AS `sequence files missing lane_index`, 'Whoops! Required lane_index is missing for some of the sequence files!!' AS message
ORDER BY process_id ASC, read_index ASC
/////////////////////////////////////////
----


[NOTE]
.Information about Graph Validation Tests
========================================
* Each cypher snippet in `.adoc` files in the specified folder will run.
* The cypher snippet should be contained between these specific lines:
```
#### The test
[source,cyper]
----
<TEST HERE>
----
```
* The test passes if the cypher query returns no results.
========================================
